#!/usr/bin/perl

=head1 NAME

mr - a Multiple Repository management tool

=head1 SYNOPSIS

B<mr> [options] checkout

B<mr> [options] update

B<mr> [options] status

B<mr> [options] commit [-m "message"]

B<mr> [options] action [params ...]

=head1 DESCRIPTION

B<mr> is a Multiple Repository management tool. It allows you to register a
set of repositories in a .mrconfig file, and then checkout, update, or
perform other actions on all of the repositories at once.

Any mix of revision control systems can be used with B<mr>, and you can
define arbitrary actions for commands like "update", "checkout", or "commit".

The predefined commands should be fairly familiar to users of any revision
control system:

=over 4

=item checkout (or co)

Checks out all the registered repositories that are not already checked
out.

=item update

Updates each registered repository from its configured remote repository.

If a repository isn't checked out yet, it will first check it out.

=item status

Displays a status report for each registered repository, showing what
uncommitted changes are present in the repository.

=item commit (or ci)

Commits changes to each registered repository. (By default, changes
are pushed to the remote repository too, when using distributed systems
like git.)

The optional -m parameter allows specifying a commit message.

=back

Actions can be abbreviated to any unambiguous subsctring, so
"mr st" is equivilant to "mr status".

=head1 OPTIONS

=over 4

=item -d directory

Specifies the topmost directory that B<mr> should work in. The default is
the current working directory. B<mr> will operate on all registered
repositories at or under the directory.

=item -c mrconfig

Use the specified mrconfig file, instead of looking for on in your home
directory.

=item -v

Be verbose.

=back

=head1 FILES

B<mr> is configured by .mrconfig files. It starts by reading the .mrconfig
file in your home directory. Each repository specified in a .mrconfig file
can also have its own .mrconfig file in its root directory that can
optionally be used as well. So you could have a ~/.mrconfig that registers a
repository ~/src, that itself contains a ~/src/.mrconfig file, that in turn
registers several additional repositories.

The .mrconfig file uses a variant of the INI file format. Lines starting with
"#" are comments. Lines ending with "\" are continued on to the next line.
Sections specify where each repository is located, relative to the
directory that contains the .mrconfig file.

Within a section, each parameter defines a shell command to run to handle a
given action. Note that these shell commands are run in a "set -e" shell
environment, where any additional parameters you pass are available in
"$@". B<mr> cds into the repository directory before running
a command, except for the "checkout" command, which is run in the parent
of the repository directory, since the repository isn't checked out yet.

There are three special parameters. If the "skip" parameter is set and
its command returns nonzero, then B<mr> will skip acting on that repository.
If the "chain" parameter is set and its command returns nonzero, then B<mr>
will try to load a .mrconfig file from the root of the repository. (You
should avoid chaining from repositories with untrusted committers.) The
"lib" parameter can specify some shell code that will be run before each
command, this can be a useful way to define shell functions other commands
can use.

The "default" section allows setting up default handlers for each action,
and is overridden by the contents of other sections. mr contains default
handlers for the "update", "status", and "commit" actions, so normally
you only need to specify what to do for "checkout".

The "alias" section allows adding aliases for commands. Each parameter
is an alias, and its value is the command to run.

For example:

  [src]
  checkout = svn co svn://svn.example.com/src/trunk src
  chain = true

  [src/linux-2.6]
  skip = small
  checkout = git clone git://git.kernel.org/pub/scm/linux/kernel/git/torvalds/linux-2.6.git

  [default]
  lib = \
  small() {
  	case "$(hostname)" in; \
  	slug|snail); \
  		return 0; ;; ; \
  	esac; \
  	return 1; \
  }

=head1 AUTHOR

Copyright 2007 Joey Hess <joey@kitenet.net>

Licensed under the GNU GPL version 2 or higher.

http://kitenet.net/~joey/code/mr/

=cut

use warnings;
use strict;
use Getopt::Long;
use Cwd qw(getcwd abs_path);

my $directory=getcwd();
my $config="$ENV{HOME}/.mrconfig";
my $verbose=0;
my %config;
my %knownactions;
my %alias;

Getopt::Long::Configure("no_permute");
my $result=GetOptions(
	"d=s" => sub { $directory=abs_path($_[1]) },
	"c=s" => \$config,
	"v" => \$verbose,
);
if (! $result || @ARGV < 1) {
	die("Usage: mr [-d directory] action [params ...]\n");
}

loadconfig(\*DATA);
loadconfig($config);
#use Data::Dumper;
#print Dumper(\%config);

# alias expansion and command stemming
my $action=shift @ARGV;
if (! exists $knownactions{$action}) {
	if (exists $alias{$action}) {
		$action=$alias{$action};
	}
	else {
		my @matches = grep { /^\Q$action\E/ }
			keys %knownactions, keys %alias;
		if (@matches == 1) {
			$action=$matches[0];
		}
		else {
			die "mr: ambiguous action \"$action\" (matches @matches)\n";
		}
	}
}

# handle being in a subdir of a repository
foreach my $topdir (sort keys %config) {
	foreach my $subdir (sort keys %{$config{$topdir}}) {
		if ($directory =~ /^\Q$topdir$subdir\E\//) {
			$directory=$topdir.$subdir;
		}
	}
}

my (@failed, @successful, @skipped);
my $first=1;
foreach my $topdir (sort keys %config) {
	foreach my $subdir (sort keys %{$config{$topdir}}) {
		next if $subdir eq 'default';
		
		my $dir=$topdir.$subdir;

		if (defined $directory &&
		    $dir ne $directory &&
		    $dir !~ /^\Q$directory\E\//) {
			next;
		}

		print "\n" unless $first;
		$first=0;

		action($action, $dir, $topdir, $subdir);

	}
}

sub action {
	my ($action, $dir, $topdir, $subdir) = @_;
	
	my $lib= exists $config{$topdir}{$subdir}{lib} ?
	                $config{$topdir}{$subdir}{lib} : "";

	if ($action eq 'checkout') {
		if (-d $dir) {
			print "mr $action: $dir already exists, skipping checkout\n" if $verbose;
			push @skipped, $dir;
			return;
		}
		$dir=~s/^(.*)\/[^\/]+\/?$/$1/;
	}
	elsif ($action eq 'update') {
		if (! -d $dir) {
			return action("checkout", $dir, $topdir, $subdir);
		}
	}
	
	if (! chdir($dir)) {
		print STDERR "mr $action: failed to chdir to $dir: $!\n";
		push @skipped, $dir;
	}

	if (exists $config{$topdir}{$subdir}{skip}) {
		my $ret=system($lib.$config{$topdir}{$subdir}{skip});
		if ($ret >> 8 == 0) {
			print "mr $action: $dir skipped per config file\n" if $verbose;
			push @skipped, $dir;
			return;
		}
	}

	if (! exists $config{$topdir}{$subdir}{$action}) {
		print STDERR "mr $action: no defined $action command for $topdir$subdir, skipping\n";
		push @skipped, $dir;
	}
	else {
		print "mr $action: in $dir\n";
		my $command="set -e; ".$lib.
			"my_action(){ $config{$topdir}{$subdir}{$action} ; }; my_action ".
			join(" ", map { s/\//\/\//g; s/"/\"/g; '"'.$_.'"' } @ARGV);
		my $ret=system($command);
		if ($ret != 0) {
			print STDERR "mr $action: failed to run: $command\n" if $verbose;
			push @failed, $topdir.$subdir;
			if ($ret >> 8 != 0) {
				print STDERR "mr $action: command failed\n";
			}
			elsif ($ret != 0) {
				print STDERR "mr $action: command died ($ret)\n";
			}
		}
		else {
			push @successful, $dir;
		}
	}
}

sub showstat {
	my $count=shift;
	my $singular=shift;
	my $plural=shift;
	if ($count) {
		return "$count ".($count > 1 ? $plural : $singular);
	}
	return;
}
print "\nmr $action: finished (".join("; ",
	showstat($#successful+1, "successful", "successful"),
	showstat($#failed+1, "failed", "failed"),
	showstat($#skipped+1, "skipped", "skipped"),
).")\n";
if (@failed) {
	exit 1;
}
elsif (! @successful && @skipped) {
	exit 1;
}
exit 0;

my %loaded;
sub loadconfig {
	my $f=shift;

	my @toload;

	my $in;
	my $dir;
	if (ref $f eq 'GLOB') {
		$in=$f;	
		$dir="";
	}
	else {
		# $f might be a symlink
		my $absf=abs_path($f);
		if ($loaded{$absf}) {
			return;
		}
		$loaded{$absf}=1;

		print "mr: loading config $f\n" if $verbose;
		open($in, "<", $f) || die "mr: open $f: $!\n";
		($dir)=$f=~/^(.*\/)[^\/]+$/;
		if (! defined $dir) {
			$dir=".";
		}
		$dir=abs_path($dir)."/";

		# copy in defaults from first parent
		my $parent=$dir;
		while ($parent=~s/^(.*)\/[^\/]+\/?$/$1/) {
			if (exists $config{$parent} &&
			    exists $config{$parent}{default}) {
				$config{$dir}{default}={ %{$config{$parent}{default}} };
				last;
			}
		}
	}

	my $section;
	while (<$in>) {
		chomp;
		next if /^\s*\#/ || /^\s*$/;
		if (/^\s*\[([^\]]*)\]\s*$/) {
			$section=$1;
		}
		elsif (/^\s*(\w+)\s*=\s*(.*)/) {
			my $parameter=$1;
			my $value=$2;

			# continuation line
			while ($value=~/(.*)\\$/) {
				$value=$1.<$in>;
				chomp $value;
			}

			if (! defined $section) {
				die "$f line $.: parameter ($parameter) not in section\n";
			}
			if ($section ne 'alias' &&
			    ! exists $config{$dir}{$section} &&
			    exists $config{$dir}{default}) {
				# copy in defaults
				$config{$dir}{$section}={ %{$config{$dir}{default}} };
			}
			if ($section eq 'alias') {
				$alias{$parameter}=$value;
			}
			elsif ($parameter eq 'lib') {
				$config{$dir}{$section}{lib}.=$value." ; ";
			}
			else {
				$config{$dir}{$section}{$parameter}=$value;
				$knownactions{$parameter}=1;
				if ($parameter eq 'chain' &&
				    length $dir && $section ne "default" &&
				    -e $dir.$section."/.mrconfig" &&
			    	    system($value) >> 8 == 0) {
					push @toload, $dir.$section."/.mrconfig";
			        }
			}
		}
		else {
			die "$f line $.: parse error\n";
		}
	}
	close $in;

	foreach (@toload) {
		loadconfig($_);
	}
}

# Finally, some useful actions that mr knows about by default.
# These can be overridden in ~/.mrconfig.
__DATA__
[alias]
	co = checkout
	ci = commit
[default]
lib = \
	error() { \
		echo "mr: $@" >&2; \
		exit 1; \
	}
update = \
	if [ -d .svn ]; then \
		svn update "$@"; \
	elif [ -d .git ]; then \
		git pull origin master "$@"; \
	else \
		error "unknown repo type"; \
	fi
status = \
	if [ -d .svn ]; then \
		svn status "$@"; \
	elif [ -d .git ]; then \
		git status "$@" || true; \
	else \
		error "unknown repo type"; \
	fi
commit = \
	if [ -d .svn ]; then \
		svn commit "$@"; \
	elif [ -d .git ]; then \
		git commit -a "$@" && git push --all; \
	else \
		error "unknown repo type"; \
	fi
